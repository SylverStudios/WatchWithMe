apply plugin: 'java'
apply plugin: 'application'

// Use Java 8 by default
sourceCompatibility = 1.8
targetCompatibility = 1.8
version = '1.0'

description = "Watch With Me"
mainClassName = "com.sylver.watchwithme.api.App"

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

buildscript {
  repositories {
    mavenCentral()
  }
}

// Set our project variables
project.ext {
  dropwizardVersion = '0.9.2'
}

jar {
  manifest {
    attributes "Implementation-Title": description
    attributes "Implementation-Version": version
    attributes "Main-Class": mainClassName
  }

  // Copy all dependencies into the jar to make a fat jar. Also remove any
  // security files (from mail.jar / activation.jar) so that the jar will be
  // executable.
  doFirst {
    from(
      configurations.runtime.resolve().collect { it.isDirectory() ? it : zipTree(it) }
    ) {
      exclude 'META-INF/MANIFEST.MF'
      exclude 'META-INF/*.SF'
      exclude 'META-INF/*.DSA'
      exclude 'META-INF/*.RSA'
    }
  }
}

repositories {
  mavenCentral()
}

dependencies {
  compile (
    'io.dropwizard:dropwizard-core:' + dropwizardVersion,
    'io.dropwizard:dropwizard-assets:' + dropwizardVersion,
    'com.liveperson:dropwizard-websockets:0.9.1-1'
  )
  testCompile group: 'junit', name: 'junit', version: '4.11'
}

// Configure the run task to start the Dropwizard service
run {
  args 'server', 'watch-with-me.yaml'
}
